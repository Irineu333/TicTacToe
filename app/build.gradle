plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdk 32

    signingConfigs {
        release {
            def keystoreProperties = new Properties()

            keystoreProperties.load(
                    new FileInputStream(
                            rootProject.file("keystore.properties")
                    )
            )

            storeFile rootProject.file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    defaultConfig {
        applicationId "com.neo.hash"
        minSdk 21
        targetSdk 32
        versionCode 8
        versionName "1.6.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {

        def adsProperties = new Properties()

        adsProperties.load(
                new FileInputStream(
                        rootProject.file("ads.properties")
                )
        )

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [ADMOB_APP_ID: adsProperties["APP_ID"]]

            buildConfigField "String", "BANNER_ID", adsProperties["BANNER_ID"]
            buildConfigField "String", "INTERSTITIAL_ID", adsProperties["INTERSTITIAL_ID"]

            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false

            versionNameSuffix '-dev'
            applicationIdSuffix ".dev"

            manifestPlaceholders = [ADMOB_APP_ID: adsProperties["DEBUG_APP_ID"]]

            buildConfigField "String", "BANNER_ID", adsProperties["DEBUG_BANNER_ID"]
            buildConfigField "String", "INTERSTITIAL_ID", adsProperties["DEBUG_INTERSTITIAL_ID"]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'

    //compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    //compose navigation
    implementation "androidx.navigation:navigation-compose:$nav_version"

    //activity compose integration
    implementation 'androidx.activity:activity-compose:1.5.1'

    // TODO: temporary, remove when navigation compose has transition animations
    implementation('com.google.accompanist:accompanist-navigation-animation:0.23.1') {
        because("navigation compose has no transition animations")
    }

    //splash
    implementation 'androidx.core:core-splashscreen:1.0.0'

    //lifecycle
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'

    //log
    implementation 'com.jakewharton.timber:timber:5.0.1'

    //firebase
    implementation platform('com.google.firebase:firebase-bom:30.3.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-database-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'

    //ads
    implementation 'com.google.android.gms:play-services-ads:21.1.0'

    //simple data persistence
    implementation 'androidx.datastore:datastore-preferences:1.0.0'

    //tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //compose tests
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}